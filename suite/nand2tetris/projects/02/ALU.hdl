// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    // preprocess x
    Mux16(a=x, b[0..15]=false, sel=zx, out=zx0); // zero the y
    Not16(in=zx0, out=nzx0); // negate y
    Mux16(a=zx0, b=nzx0, sel=nx, out=zx1);

    // preprocess y
    Mux16(a=y, b[0..15]=false, sel=zy, out=zy0); // zero the y
    Not16(in=zy0, out=nzy0); // negate y
    Mux16(a=zy0, b=nzy0, sel=ny, out=zy1);

    // x plus y / x and y
    Add16(a=zx1, b=zy1, out=xplusy); 
    And16(a=zx1, b=zy1, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=out0);

    // set out
    Not16(in=out0, out=nout0);
    Mux16(
      a=out0, b=nout0, sel=no, 
      out=out, 
      out[0..7]=halfa, 
      out[8..15]=halfb, 
      out[15]=signbit
    );

    // set zr
    Or8Way(in=halfa, out=halfa0);
    Or8Way(in=halfb, out=halfb0);
    Or(a=halfa0, b=halfb0, out=nzr);
    Not(in=nzr, out=zr);

    // set ng
    And(a=signbit, b=true, out=ng);
}